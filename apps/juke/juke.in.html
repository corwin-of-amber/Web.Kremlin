<meta charset="utf-8">
<script src="node_modules/nwjs-kremlin/reload.js"></script>
<%= jquery />
<%= angular />

<script type="text/ls">
#BASE_URL = "http://localhost:2222"
BASE_URL = "http://192.168.0.7:2222"

app = angular.module "app" []
  ..controller \Ctrl ($scope) ->
    $scope.tracks = []
    $scope.albums = []
    $scope.artists = []
    $scope.select = {}
     
    $scope.volume = 275
    $scope.max-volume = 500
    
    $scope.base-url = BASE_URL
    
    $scope.play = (ids) ->
      if $.isArray(ids) then ids = ids.join(",")
      data <-! $.post @base-url + "/play", ""+ids
      console.log "play: " + data
    $scope.stop = ->
      data <-! $.post @base-url + "/stop"
      console.log data
    
    $scope.cu = (d) ->
      {}
        for k,v of d
          if v? then ..[k] = v
    
    $scope.slice = (viewed, index) ->
      [t._id for t in viewed[index to]]
    
    $scope.reload-tracks = ->
      $.ajax do
        type: \GET
        contentType: "application/json;charset=utf-8"
        url: @base-url
        success: (data) !->
          <- $scope.$apply
          $scope.tracks = data
          $scope.albums = project data, 'album'
          $scope.artists = project data, 'artist', 2
    
    $scope.reload-tracks!
    
    $scope.start-upload = -> upload @base-url, @~reload-tracks
    
    $scope.$watch \volume (volume) ->
      max = $scope.max-volume
      $.get "#{$scope.base-url}/vol?#volume/#max"
      
  ..filter \project ->
    (tracks, [column, th]) -> project tracks, column, th
    
  ..filter \tracknum ->
    (value) -> if value ~= 0 then "" else value
    
uniq = (values, threshold=1) -> []
  hist = {}
  for v in values
    if (hist[v] = (hist[v] ? 0) + 1) == threshold then ..push v
    
project = (tracks, column, threshold) ->
  uniq [..[column] for tracks], threshold
  
upload = (base-url, reload-cb) ->
  fe = document.forms.upload.elements
  fd = new FormData
  $.each fe.upload.files, (i, file) ->
    fd.append 'payload', file
  qs = if fe.play.checked then "?play" else ""
  $.ajax do
    url: "#{base-url}/upload#qs"
    type: \POST
    data: fd
    processData: false
    contentType: false
    xhr: ->
      xhr = $.ajax-settings.xhr!
      if xhr.upload
        acc = 1
        xhr.upload.addEventListener \progress (e) ->
          pc = (Math.round 100 * acc * e.loaded / e.total) / acc
          if pc < 1 then acc := 10
          $ \#percentage .text "#pc%"
      xhr
    success: (data) !->
      $ \#percentage .text ""
      reload-cb!
      console.log data
    error: (xhr, status, error) !->
      console.log "Upload failed."
      console.log "Reason: #status - #error"
      
</script>

<style>
  .hi { 
    background-color: rgba(0,0,255,0.2) !important; 
  }
  #picker {
    display: flex;
    align-items: flex-start;
  }
  ul { 
    border: 1px solid green; 
    min-width: 150px; 
    padding-left: 0px;
    display: inline-block;
  }
  ul li {
    padding-left: 5px;
    padding-right: 5px;
    white-space: nowrap;
    overflow: hidden;
    list-style-position: none;
  }
  ul li:nth-child(even) { 
    background: rgba(0,0,0,0.08); 
  }
</style>

<body ng-app="app"
  ng-controller="Ctrl">
  <div><input ng-model="baseUrl" list="recentUrls">
  <button id="reload" ng-click="reloadTracks()">Reload</button>
  </div>
  <div id="picker">
  <ul>
    <li ng-repeat="artist in tracks | project:['artist',2]" 
      ng-click="select.artist=artist"
      ng-class="{hi: select.artist==artist}">
      {{artist}}
    </li>
    <li ng-click="select.artist=undefined"
      ng-class="{hi: !select.artist}">
      [all]</li>
  </ul>
  <ul>
    <li ng-repeat="album in tracks 
      | filter:cu({artist: select.artist})
      | project:['album']" 
      ng-click="select.album=album"
      ng-class="{hi: select.album==album}">
      {{album}}
    </li>
    <li ng-click="select.album=undefined"
      ng-class="{hi: !select.album}">
      [all]</li>
  </ul>
  </div>
  
  <div style="clear: both">
    <span ng-click="stop()">[stop]</span>
    <input type="range" 
      min="0" max="9999"
      ng-attr-max="{{maxVolume}}" ng-model="volume">
    <!-- set max to 9999 initially to avoid clipping
      before ng-attr-max kicks in -->
  </div>
  
  <table style="clear: left">
    <tr ng-repeat="track in tracks 
           | filter:cu(select):true as viewed"
        ng-click="play(slice(viewed, $index))">
      <td></td>
      <td>{{track.track | tracknum}}</td>
      <td>{{track.title}}</td>
      <td>{{track._id}}</td>
    </tr>
  </table>

  <form name="upload">
    <input id="upload" type="file" multiple>
    <input id="button" type="button" value="Upload" ng-click="startUpload()">
    <span id="percentage"></span><br/>
    <input type="checkbox" name="play">play</input>
  </form>
  
  <datalist id="recentUrls">
    <option value="http://192.168.0.7:2222"></option>
    <option value="http://192.168.0.8:2222"></option>
    <option value="http://localhost:2222"></option>
  </datalist>    
</body>

<%= livescript />
